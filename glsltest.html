<html><head><meta http-equiv="Content-Type" content="text/html; charset=UTF-8"><!-- fragment shader --><script id="fs" type="x-shader/x-fragment">precision mediump float;
uniform float time;
uniform vec2  mouse;
uniform vec2  resolution;
uniform sampler2D texture0;
uniform sampler2D texture1;

const vec3 cPos = vec3(0.0,  7.0,  5.0);
const vec3 cDir = vec3(0.0,  0.0, -1.0);
const vec3 cUp  = vec3(0.0,  1.0,  0.0);

// 青空対応
varying vec3 v_position;

// 処理対象のタイルID
uniform int tileX;
uniform int tileY;

// テクスチャ座標への変換
float xToU(float x) {
    return (x + 12.0) * 0.1;
}
float zToV(float z) {
    return (z + 30.0) * 0.1;
}

// レイマーチングの距離関数
float distFunc(vec3 p){
    float u = xToU(p.x) - float(tileX);
    float v = zToV(p.z) - float(tileY);

    // u,vが処理対象タイルの範囲外なら，スキップする
    if (v < 0.0 || v > 1.0) {
        return 0.5;
    }
    if (u < 0.0 || u > 1.0) {
        return 0.5;
    }

    // PNG標高タイルを解析する
    vec4 altColor = texture2D(texture0, vec2(u, v));

	float altI = (
        altColor.r * 255.0 * 65536.0 
        + altColor.g * 255.0 * 256.0
        + altColor.b * 255.0);

    float alt;
    if (altI < float(0x00800000)) {
        alt = altI / 100.0;
    } else if (altI == float(0x00800000)) {
        // 欠測はとりあえず0として扱う
        alt = 0.0;
    } else {
        alt = (altI - float(0x01000000)) / 100.0;
    }

    return p.y - alt * 0.01;
}

void main(void){
    // fragment position
    // [-1, 1]
    vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);

	// mouse
    vec2 m = vec2(mouse.x * 2.0 - 1.0, -mouse.y * 2.0 + 1.0);

    // マウス座標によるカメラ移動
	vec3 cmPos = vec3(cPos.x - m.x * 20.0, cPos.y - m.y * 6.0, cPos.z);

    // camera and ray
    vec3 cSide = cross(cDir, cUp);
    float targetDepth = 1.0;
    vec3 ray = normalize(cSide * p.x + cUp * p.y + cDir * targetDepth);
    
    vec4 groundColor;

    // マーチングループ
    float rayLen = 0.0;
    bool reached = false;

    for(int i = 0; i < 150; i++){
        vec3 dPos = cmPos + ray * rayLen;

        float dist = distFunc(dPos);

        if (abs(dist) <= 0.1) {
            reached = true;
            // 地面画像タイルを読み込む
            float u = xToU(dPos.x) - float(tileX);
            float v = zToV(dPos.z) - float(tileY);
            groundColor = texture2D(texture1, vec2(u, v));
			break;
	    }

        rayLen += dist * 0.5;
    }

    if (reached) {
        gl_FragColor = groundColor;

    } else {
        if (tileX == 0 && tileY == 0) {
            // 青空対応
            // 初回の呼び出しでは余白に青空グラデーションを描画する
            // y = 1 (上)   ->  y = 0 (中央)
            // (0, 0.75, 1) -> (0.6, 0.85, 1)
            float y = v_position.y;
            gl_FragColor = vec4(0.6 - (y * 0.6), 0.85 - (y * 0.1), 1.0, 1.0);

        } else {
            // 2回目以降の呼び出しでは，上書きしないように何も行わない
            discard;
        }
    }
}

</script><!-- /fragment shader -->

<!-- vertex shader --><script id="vs" type="x-shader/x-vertex">
attribute vec3 position;
varying vec3 v_position;

void main(void){
	// 青空対応
    v_position = position;

	gl_Position = vec4(position, 1.0);
}
</script><!-- /vertex shader -->

<script src="glsltest.js" type="text/javascript"></script>
<style type="text/css">
	* {
		text-align: center;
		margin: 10px auto;
		padding: 0px;
	}
	canvas {
		box-shadow: 0px 0px 0px 1px gray;
	}
</style>
<title>GLSLによるPNG標高タイルの3D化実験</title>
</head>
<body>
<h2>GLSLによるPNG標高タイルの3D化実験</h2>
<p>
マウス移動でカメラ位置を移動します．
</p>
<canvas id="canvas"></canvas>
<p>
出典：<a href="https://maps.gsi.go.jp/development/ichiran.html">国土地理院</a>[シームレス空中写真][標高タイル]
</p>
<p>
参考：<a href="https://wgld.org/d/glsl/">wgld.org | GLSL</a>
</p>

</body>
</html>
